class JMeterRule implements ComponentMetadataRule {
	void execute(ComponentMetadataContext context) {
		context.details.allVariants {
			withDependencies {
				removeAll { it.group == "org.apache.jmeter" && it.name == "bom" }
			}
		}
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 11

version = '1.0'
def title = 'JMeterInfluxDBListener'

jar {
    manifest {
        attributes 'Implementation-Title': title,
                   'Implementation-Version': version
    }
}

repositories {
  maven{
  	url "https://repo.maven.apache.org/maven2"
  	metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
  }
}
dependencies {
	compileOnly group: 'org.apache.jmeter', name: 'ApacheJMeter_core', version: '5.4'
	compileOnly group: 'org.apache.jmeter', name: 'ApacheJMeter_components', version: '5.4'
	compile group: 'org.influxdb', name: 'influxdb-java', version: '2.4'
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
	manifest {
		attributes 'Implementation-Title': title,
			'Implementation-Version': version
	}
	baseName = project.name + '-plugin'
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

task copyTask(type: Copy) {
        from 'build/libs/.'
        into './'
}

task packageDistribution(type: Zip) {
    archiveFileName = "toilatester-jmeter-plugin.zip"
    destinationDirectory = file("$buildDir/../")

    from "$buildDir/libs"
}

task releasePlugin{
    dependsOn 'clean'
    dependsOn 'fatJar'
    dependsOn 'copyTask'
    dependsOn 'packageDistribution'
    tasks.findByName('fatJar').mustRunAfter 'clean'
    tasks.findByName('copyTask').mustRunAfter 'fatJar'
    tasks.findByName('packageDistribution').mustRunAfter 'fatJar'
    
}
